/*
 * Copyright 2019 ACINQ SAS
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

android {
  compileSdkVersion 28
  buildToolsVersion '28.0.3'
  defaultConfig {
    applicationId "fr.acinq.eclair.wallet.dev"
    minSdkVersion 21
    targetSdkVersion 28
    versionCode 45
    versionName "0.4.5-DEV"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    multiDexEnabled true
  }
  buildTypes {
    release {
      resValue "string", "CHAIN", CHAIN_TESTNET
      buildConfigField "String", "CHAIN", CHAIN_TESTNET
      debuggable false
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    debug {
      resValue "string", "CHAIN", CHAIN_TESTNET
      buildConfigField "String", "CHAIN", CHAIN_TESTNET
      debuggable true
    }
    applicationVariants.all { variant ->
      variant.outputs.all { output ->
        final String chain = variant.buildType.resValues.get("CHAIN").value // careful, contains " characters...
        outputFileName = "${defaultConfig.versionCode}-v${defaultConfig.versionName}-${chain.substring(1, chain.length() - 1)}-${variant.buildType.name}.apk"
      }
    }
  }
  configurations.all {
    resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
  }
  dataBinding {
    enabled = true
  }
  packagingOptions {
    exclude 'META-INF/LICENSE*'
    exclude 'META-INF/NOTICE.txt'
    merge 'reference.conf'
  }
  externalNativeBuild {
    cmake {
      path 'CMakeLists.txt'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

greendao {
  // increment when changing DB structure
  schemaVersion 8
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  implementation 'com.android.support:design:28.0.0'
  implementation 'com.android.support:preference-v7:28.0.0'
  implementation 'com.android.support:preference-v14:28.0.0'
  implementation 'com.android.support:support-media-compat:28.0.0'
  implementation 'com.android.support:support-v4:28.0.0'
  implementation 'com.android.support.constraint:constraint-layout:1.1.3'
  implementation 'com.google.code.findbugs:jsr305:3.0.2'
  implementation 'com.google.android.gms:play-services-drive:16.0.0'
  implementation 'com.google.android.gms:play-services-auth:16.0.1'
  implementation('android.arch.work:work-runtime:1.0.0') {
    exclude group: 'com.google.guava', module: 'listenablefuture'
  }
  // logging
  implementation 'org.slf4j:slf4j-api:1.7.25'
  implementation 'com.github.tony19:logback-android:1.1.1-12'
  implementation('com.papertrailapp:logback-syslog4j:1.0.0') {
    exclude group: 'ch.qos.logback'
  }
  // eclair core
  implementation 'fr.acinq.eclair:eclair-core_2.11:0.2-android-beta22'
  // database orm
  implementation 'org.greenrobot:greendao:3.2.2'
  // required for greendao encryption, disabled for now
  implementation 'net.zetetic:android-database-sqlcipher:3.5.6'
  // qr codes
  implementation 'com.journeyapps:zxing-android-embedded:3.6.0'
  // events
  implementation 'org.greenrobot:eventbus:3.0.0'
  // http client
  testImplementation 'junit:junit:4.12'
  androidTestImplementation('com.typesafe.akka:akka-testkit_2.11:2.3.14') {
    exclude group: 'org.scala-lang', module: 'scala-library'
  }
}
